plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

repositories {
    mavenLocal()
    mavenCentral()
}

group 'com.cdac'
version '1.0'


def junitVersion = '5.9.1'
def lombokVersion = '1.18.24'
def jarDir = '/usr/share/enrollment/lib/'


sourceCompatibility = '11'
targetCompatibility = '11'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.cdac.enrollmentstation'
    mainClass = 'com.cdac.enrollmentstation.App'
}

javafx {
//    version = '13'
    version = '13'
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.web', 'javafx.swing']
}

dependencies {
    implementation fileTree(dir: 'libs', include: '*.jar')
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"

    implementation 'com.cedarsoftware:json-io:4.12.0'
    implementation 'commons-io:commons-io:2.8.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.1'
    implementation 'net.java.dev.jna:jna:4.2.2'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.60' // latest version not supported
    implementation 'org.json:json:20160810'
    implementation 'org.controlsfx:controlsfx:8.40.11'
//    implementation 'jakarta.activation:jakarta.activation-api:1.2.1'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:2.3.2'


//    implementation 'org.json:json:20160810'
//    implementation 'io.opentracing:opentracing-api:0.33.0'
//    implementation 'com.google.code.gson:gson:2.8.5'
//    implementation 'commons-codec:commons-codec:1.15' // transitively imported by MFS jar


    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}


jar {
    manifest {
        attributes("Main-Class": "com.cdac.enrollmentstation.App")
        attributes("Class-Path": configurations.runtimeClasspath.files.collect { jarDir + it.getName() }.join(' '))
    }
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
    }
}

jlinkZip {
    group = 'distribution'
}

// custom Task
task copyDependencies(type: Copy) {
    group "copy dependencies"
    description "Copy dependencies to ${jarDir}"
    configurations.runtimeClasspath.files.collect {
        it.getAbsolutePath()
    }.forEach {
        from(it)
        into jarDir
    }
}